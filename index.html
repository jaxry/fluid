<!DOCTYPE html>
<html class='no-js'>
    <head>
        <meta charset='utf-8'>
        <meta http-equiv='X-UA-Compatible' content='IE=edge,chrome=1'>
        <title>Fluid</title>
        <meta name='description' content=''>
        <meta name='viewport' content='width=device-width'>

        <link rel='stylesheet' href='css/normalize.min.css'>
        <link rel='stylesheet' href='css/main.css'>

        <script src='js/vendor/stats.js'></script>
        <script src='js/vendor/dat.gui.min.js'></script>

        <script>
          (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
          (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
          m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
          })(window,document,'script','//www.google-analytics.com/analytics.js','ga');

          ga('create', 'UA-48809051-2', 'auto');
          ga('send', 'pageview');

        </script>
    </head>
    <body>

        <canvas id='canvas'></canvas>

        <script id='vertex-shader' type='x-shader/x-vertex'>
            attribute vec2 a_offset;
            attribute vec2 a_position;

            varying vec2 v_texCoord;
            varying vec2 v_offset;

            void main() {
                vec2 clipSpace = a_position * 2.0 - 1.0;
               
                gl_Position = vec4(clipSpace, 0, 1);

                v_texCoord = a_position;
                v_offset = a_offset;
            }
        </script>
        <script id='add-force-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform vec2 u_mouse;
            uniform vec2 u_mouseDelta;
            uniform float u_speed;
            uniform float u_gridScale;
            uniform vec2 u_resolution;
            uniform sampler2D u_velocity;

            varying vec2 v_texCoord;

            float brush = 1.0;

            float strength = u_speed * u_gridScale / 1100.0;
            float radius = u_speed * u_gridScale / 150.0;

            void main() {

                float d = length(u_resolution * (u_mouse - v_texCoord));

                vec2 color = mix(u_mouseDelta * strength, vec2(0, 0), 
                                 smoothstep(0.0, radius, d));

                gl_FragColor = vec4(color + texture2D(u_velocity, v_texCoord).xy, 0, 1);

            }
        </script>
        <script id='diffuse-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform vec2 u_onePixel;
            uniform float u_gridScale;
            uniform sampler2D u_velocity;

            varying vec2 v_texCoord;

            float a = u_gridScale * u_gridScale * 0.0000001;

            vec2 vAt(int x, int y) {
                return texture2D(u_velocity, v_texCoord + u_onePixel * vec2(x, y)).xy;
            }

            void main() {

                vec2 diffuse = (vAt(0, 0) + a*(vAt(1, 0) + vAt(-1, 0) + vAt(0, 1) + vAt(0, -1))) / (1.0 + 4.0*a);

                gl_FragColor = vec4(diffuse, 0, 1);
            }
        </script>
        <script id='advect-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform vec2 u_aspectRatio;
            uniform float u_gridScale;
            uniform float u_viscosity;
            uniform sampler2D u_velocity;

            varying vec2 v_texCoord;
            
            void main() {

                vec2 newPos = v_texCoord - texture2D(u_velocity, v_texCoord).xy * u_aspectRatio / u_gridScale / u_viscosity;

                gl_FragColor = texture2D(u_velocity, newPos);
            }
        </script>
        <script id='divergence-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform vec2 u_onePixel;
            uniform float u_gridScale;
            uniform sampler2D u_velocity;

            varying vec2 v_texCoord;

            float h = -0.5 / u_gridScale;

            void main() {

                float x1 = texture2D(u_velocity, v_texCoord + u_onePixel * vec2(1, 0)).x;
                float x0 = texture2D(u_velocity, v_texCoord + u_onePixel * vec2(-1, 0)).x;
                float y1 = texture2D(u_velocity, v_texCoord + u_onePixel * vec2(0, 1)).y;
                float y0 = texture2D(u_velocity, v_texCoord + u_onePixel * vec2(0, -1)).y;

                float divergence =  h * (x1 - x0 + y1 - y0);
                gl_FragColor = vec4(divergence);
            }
        </script>
        <script id='poisson-jacobi-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform sampler2D u_pressure;
            uniform sampler2D u_divergence;
            uniform vec2 u_onePixel;
            uniform float u_gridScale;
            varying vec2 v_texCoord;

            void main() {

                float x1 = texture2D(u_pressure, v_texCoord + u_onePixel * vec2(1, 0)).r;
                float x0 = texture2D(u_pressure, v_texCoord + u_onePixel * vec2(-1, 0)).r;
                float y1 = texture2D(u_pressure, v_texCoord + u_onePixel * vec2(0, 1)).r;
                float y0 = texture2D(u_pressure, v_texCoord + u_onePixel * vec2(0, -1)).r;

                float pressure = (x1 + x0 + y1 + y0 + texture2D(u_divergence, v_texCoord).r ) / 4.0;
                gl_FragColor = vec4(pressure);
            }
        </script>
        <script id='gradient-subtract-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform sampler2D u_velocity;
            uniform sampler2D u_pressure;
            uniform vec2 u_onePixel;
            uniform float u_gridScale;
            varying vec2 v_texCoord;

            float h = 0.5 * u_gridScale;

            void main() {

                float x1 = texture2D(u_pressure, v_texCoord + u_onePixel * vec2(1, 0)).r;
                float x0 = texture2D(u_pressure, v_texCoord + u_onePixel * vec2(-1, 0)).r;
                float y1 = texture2D(u_pressure, v_texCoord + u_onePixel * vec2(0, 1)).r;
                float y0 = texture2D(u_pressure, v_texCoord + u_onePixel * vec2(0, -1)).r;

                vec2 gradient = h * vec2(x1 - x0, y1 - y0);
                gl_FragColor = vec4(texture2D(u_velocity, v_texCoord).xy - gradient, 0, 1);
            }
        </script>
        <script id='boundary-condition-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform float u_scale;
            uniform sampler2D u_velocity;
    
            varying vec2 v_texCoord;
            varying vec2 v_offset;
            
            void main() {

                gl_FragColor = u_scale * texture2D(u_velocity, v_texCoord + v_offset);
            }
        </script>
        <script id='screen-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform vec3 u_c1;
            uniform vec3 u_c2;
            uniform vec3 u_c3;
            uniform vec3 u_c4;
            uniform vec3 u_c5;
            uniform float u_gridScale;
            uniform sampler2D u_velocity;
            uniform sampler2D u_pressure;
            
            varying vec2 v_texCoord;

            float scale = 9.0;

            void main() {

                vec2 v = texture2D(u_velocity, v_texCoord).xy;
                float p = texture2D(u_pressure, v_texCoord).r;

                vec3 c = mix(mix(u_c3, u_c4, smoothstep(-scale, scale, v.x)),
                             mix(u_c1, u_c2, smoothstep(-scale, scale, v.x)), smoothstep(-scale, scale, v.y));
                
                c += u_c5 * p * scale * 2.0;
               
                // float sp = step(0.0, sign(p));
                // c = mix(c, sp*u_c5 - (1.0 - sp)*u_c5, abs(p * 15.0));

                gl_FragColor = vec4(c, 1);
            }


        </script>
        <script id='conserve-pressure-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform float u_scale;
            uniform sampler2D u_pressure;

            varying vec2 v_texCoord;

            void main() {

                gl_FragColor = texture2D(u_pressure, v_texCoord) * u_scale;
            }
        </script>
        <script src='js/main.js'></script>
    </body>
</html>
